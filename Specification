FSQL : Specification -- Language specification                #
                                                              #
Copyright (C) 2015 gahag                                      #
All rights reserved.                                          #
                                                              #
This software may be modified and distributed under the terms #
of the BSD license.  See the LICENSE file for details.        #
                                                              #
###############################################################

FSQL - File System Query Language

FSQL is a tool for performing queries on the local filesystem
with syntax similar to SQL.

Any filepath/filename containing one of the characters
" '!<>&|=(),
must be quoted with double quotes. Double quotes inside
quoted identifiers must me escaped with backslash(\).

###############################################################

Data keywords:
name : file name
date : file modification date
size : file size

The complete FSLQ grammar:
  <chars> ::= <char>
            | <char><chars>
  
  
  <identifier> ::= <chars> !! none of < " '!<>&|=(), >
  <string>     ::= "<chars>"
  <ident>      ::= <identifier>
                 | <string>
  
  <selection>  ::= name | date | size
  <selections> ::= <selection> , <selections>
                 | <selection>
  <join_type>  ::= left | full | inner | outer | right
  
  <not_op>          ::= !
  <bool'bool_ops>   ::= && | ||
  <name'ident'_ops> ::= =~
  <ord'ord'_ops>    ::= == | != | <= | >= | < | >
  
  
  <select> ::= select <selections>
  
  <source> ::= <source_rec>
             | <source_non_rec>
  <source_rec>      ::= recursive <source_non_rec>
  <source_non_rec>  ::= <source_simple> | <source_join>
  <source_simple>   ::= from <ident>
  <source_join>     ::= <source_simple> <join_type> join <ident> on <selection>
  
  <where> ::= where <expr>
  
  <expr> ::= (<expr>)
           | <not_op> <expr>
           | <rel_expr>
           | <expr> <bool'bool'_ops> <expr>
  
  <rel_expr> ::= <ident>     <ord'ord'_ops> <selection>
               | <selection> <ord'ord'_ops> <ident>
               | name <name'ident'_ops> <ident>
  
  
  <query> ::= <select> <source>
            | <select> <source> <where>

###############################################################

Examples:
  Standard select clause
    select name from dir
  
  Standard recursive clause
    select name recursive from dir
  
  Standard where clause
    select date from dir where name == file && date == 2015-12-30 || name > t
  
  Standard inner join clause
    select name from dir inner join dir' on date
  
  Multiple selections
    select name, date from dir
